###################################################################
#
# Copyright (C) 2011,  Ralf Hemmecke <ralf@hemmecke.de>
#
###################################################################
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
###################################################################
AC_PREREQ(2.68)
AC_INIT(
  [Flint],
  [2.2],
  [hart_wb@yahoo.com])

# Sanity check
AC_CONFIG_SRCDIR([doc/latex/flint-manual.tex])

# Certain configuration files are kept in the config subdirectory.
AC_CONFIG_AUX_DIR(config)
AC_CONFIG_MACRO_DIR(config)

###################################################################
# Init Automake (non-gnu standard) 1.11
AM_INIT_AUTOMAKE([1.11 -Wall -Werror foreign subdir-objects])

###################################################################
# Use libtool version 2.4
LT_INIT
LT_PREREQ([2.4])

###################################################################
# The following was autogenerated by autoscan.
###################################################################
# Checks for programs.
AC_PROG_CXX
AC_PROG_AWK
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET

# Checks for header files.
AC_CHECK_HEADERS([float.h limits.h stdlib.h string.h sys/time.h unistd.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_PID_T
AC_TYPE_SIZE_T

# Checks for library functions.
AC_FUNC_FORK
AC_FUNC_MALLOC
AC_FUNC_REALLOC
AC_CHECK_FUNCS([gettimeofday pow sqrt strcspn strspn])
###################################################################
# End of autoscan part.
###################################################################

###################################################################
# This is suggested together with AM_INIT_AUTOMAKE([subdir-objects]).
AM_PROG_CC_C_O

###################################################################
# Check for standard math library
# Look also at the LT_LIB_M macro!
AC_CHECK_LIB([m], [cos])

###################################################################
# Check for MPIR library.
# First test whether --with-mpir was given on the command line.
# Only if --with-mpir was not given, we check for existance in
# a standard place.
AC_ARG_WITH([mpir],
  [AS_HELP_STRING(
    [--with-mpir],
    [Specify directory D of where the MPIR library is installed.
     This directory is expected to have subdirectories $D/include and $D/lib.]
  )],
  # Action if --with-mpir is given.
  # We try to compile a small program with -L$D/lib and -I$D/include
  # given additionally on the command line.
  [
  flint_save_CFLAGS=$CFLAGS
  mpir_cflags="-I$withval/include -L$withval/lib $CFLAGS"
  CFLAGS="$mpir_cflags"
  AC_CHECK_LIB(mpir, __gmpz_init, ,
    [AC_MSG_ERROR([MPIR not found, see http://www.mpir.org/])])
  CFLAGS=$flint_save_CFLAGS
  ],
  [# Action if --with-mpir is not given.
  AC_CHECK_LIB(mpir, __gmpz_init, ,
    [AC_MSG_ERROR([MPIR not found, see http://www.mpir.org/])])
  ]
)
AC_SUBST(mpir_cflags)

###################################################################
# Check for MPFR library.
# First test whether --with-mpfr was given on the command line.
# Only if --with-mpfr was not given, we check for existance in
# a standard place.
flint_save_CFLAGS=$CFLAGS
AC_ARG_WITH([mpfr],
  [AS_HELP_STRING(
    [--with-mpfr],
    [Specify directory D of where the MPIR library is installed.
     This directory is expected to have subdirectories $D/include and $D/lib.]
  )],
  # Action if --with-mpfr is given.
  # We try to compile a small program with -L$D/lib and -I$D/include
  # given additionally on the command line.
  [
  flint_save_CFLAGS=$CFLAGS
  mpfr_cflags="-I$withval/include -L$withval/lib"
  CFLAGS="$mpfr_cflags $mpir_cflags"
  AC_CHECK_LIB(mpfr, mpfr_add, , [AC_MSG_ERROR(
    [Need MPFR either from GNU MP 4 or separate MPFR package.
     See http://www.mpfr.org or http://swox.com/gmp])])
  ],[
  # Action if --with-mpir is not given.
  CFLAGS="$mpir_cflags"
  AC_CHECK_LIB(mpfr, mpfr_add, , [AC_MSG_ERROR(
    [Need MPFR either from GNU MP 4 or separate MPFR package.
     See http://www.mpfr.org or http://swox.com/gmp])])
  ]
)
CFLAGS=$flint_save_CFLAGS
AC_SUBST(mpfr_cflags)

###################################################################
# The Makefiles
AC_CONFIG_FILES([Makefile])

###################################################################
# Output everything
AC_OUTPUT
